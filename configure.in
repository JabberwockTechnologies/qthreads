#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)
AC_INIT(qthread, 0.1, rcmurph@sandia.gov)
AM_INIT_AUTOMAKE(qthread, 0.1)
AC_CONFIG_SRCDIR([main.c])
AM_CONFIG_HEADER([config.h])

AC_CANONICAL_HOST

case "$host" in
	*-solaris*)
	AC_DEFINE(__MAKECONTEXT_V2_SOURCE, 1, [force the Sun makecontext to behave correctly])
	;;
	*-linux-*)
	AC_DEFINE(NEED_RLIMIT, 1, [whether the library should use get/set-rlimit functions])
	;;
esac

AC_DEFINE(_GNU_SOURCE, 1, [make pthreads build correctly])
AC_DEFINE(_FILE_OFFSET_BITS, 64, [allow big files])

# Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AM_PROG_AS
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL
AC_PROG_RANLIB

QTHREAD_EXTRA_OBJS=""

# Checks for libraries.
# FIXME: Replace `main' with a function in `-ldl':
AC_CHECK_LIB([dl], [main])
AC_CHECK_LIB([pthread], [pthread_create], , AC_MSG_ERROR([Need pthreads]))
AC_CHECK_LIB([cprops], [cp_hashtable_create], , AC_MSG_ERROR([Need libcprops 0.1.6 or better. See http://cprops.sf.ne/]))

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h fcntl.h ucontext.h])

case "$host" in
    *-linux-*)
    AC_CHECK_HEADERS([sys/time.h sys/resource.h], , AC_MSG_ERROR(["The headers sys/time.h and sys/resource.h are required for efficient threads on Linux."]))
    AC_CHECK_FUNCS([getrlimit setrlimit], , AC_MSG_ERROR(["The functions getrlimit and setrlimit required for efficient threads on Linux."]))
    ;;
esac

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_C_VOLATILE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
headers='
#include <pthread.h>
'
AC_COMPILE_CHECK_SIZEOF(pthread_mutex_t, $headers, 24 40 44 64)
AC_DEFINE(PTHREAD_MUTEX_SMALL_ENOUGH, 1, [this signifies that pthread_mutex_t is small enough to fit in the existing data structures])

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_VPRINTF
AC_FUNC_MMAP
AC_CHECK_FUNCS([strtol floor memset munmap])
AC_CHECK_FUNCS([getcontext swapcontext makecontext], ac_cv_context_funcs=yes, ac_cv_context_funcs=no)

if test "$ac_cv_context_funcs" == "yes" ; then
	AC_DEFINE(HAVE_CONTEXT_FUNCS, 1, [the system hsas its own getcontext functions])
else
	QTHREAD_EXTRA_OBJS="$QTHREAD_EXTRA_OBJS context.lo asm.lo"
	AC_CHECK_FUNCS([memmove], , AC_MSG_ERROR(["A functional memmove is required if you do not have the get/swap/make-context functions."]))
	AC_HEADER_SYS_WAIT
	AC_TYPE_PID_T
fi

AC_SUBST(QTHREAD_EXTRA_OBJS)

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
